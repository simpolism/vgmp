plugins {
    alias(libs.plugins.android.application)
    alias(libs.plugins.kotlin.android)
}

android {
    namespace = "org.vlessert.vgmp"
    compileSdk = 35

    val keystorePropertiesFile = file("keystore.properties")
    val keystoreMap = if (keystorePropertiesFile.exists()) {
        keystorePropertiesFile.readLines()
            .filter { it.contains("=") }
            .associate {
                val (key, value) = it.split("=", limit = 2)
                key.trim() to value.trim()
            }
    } else {
        emptyMap()
    }

    signingConfigs {
        create("release") {
            if (keystoreMap.isNotEmpty()) {
                storeFile = file(keystoreMap["storeFile"] ?: "vgmp.keystore")
                storePassword = keystoreMap["storePassword"] ?: ""
                keyAlias = keystoreMap["keyAlias"] ?: "vgmp"
                keyPassword = keystoreMap["keyPassword"] ?: ""
            } else {
                throw GradleException("keystore.properties not found. Create it with your signing credentials.")
            }
        }
    }

    configurations.all {
        resolutionStrategy {
            force("org.jetbrains.kotlin:kotlin-stdlib:2.0.0")
            force("org.jetbrains.kotlin:kotlin-stdlib-jdk8:2.0.0")
            force("org.jetbrains.kotlin:kotlin-stdlib-jdk7:2.0.0")
        }
    }

    defaultConfig {
        applicationId = "org.vlessert.vgmp"
        minSdk = 26
        targetSdk = 35
        versionCode = 1
        versionName = "1.0"

        testInstrumentationRunner = "androidx.test.runner.AndroidJUnitRunner"

        ndk {
            abiFilters += listOf("arm64-v8a", "x86_64")
        }

        externalNativeBuild {
            cmake {
                cppFlags += listOf("-std=c++14", "-DHAVE_STDINT_H", "-DVGM_LITTLE_ENDIAN",
                    "-Wno-unused-parameter", "-Wno-sign-compare", "-Wno-unused-variable",
                    "-Wno-unused-function", "-Wno-unknown-pragmas")
                arguments += listOf(
                    "-DBUILD_LIBAUDIO=OFF",
                    "-DBUILD_PLAYER=OFF",
                    "-DBUILD_VGM2WAV=OFF",
                    "-DBUILD_TESTS=OFF",
                    "-DUSE_SANITIZERS=OFF"
                )
            }
        }
    }

    externalNativeBuild {
        cmake {
            path = file("src/main/cpp/CMakeLists.txt")
            version = "3.22.1"
        }
    }

    buildTypes {
        release {
            signingConfig = signingConfigs.getByName("release")
            isMinifyEnabled = false
            proguardFiles(
                getDefaultProguardFile("proguard-android-optimize.txt"),
                "proguard-rules.pro"
            )
        }
    }

    compileOptions {
        sourceCompatibility = JavaVersion.VERSION_17
        targetCompatibility = JavaVersion.VERSION_17
    }

    kotlin {
        compilerOptions {
            jvmTarget.set(org.jetbrains.kotlin.gradle.dsl.JvmTarget.JVM_17)
        }
    }

    buildFeatures {
        viewBinding = true
    }
}

dependencies {
    implementation(libs.androidx.core.ktx)
    implementation(libs.androidx.appcompat)
    implementation(libs.material)
    implementation(libs.androidx.activity)
    implementation(libs.androidx.constraintlayout)
    implementation(libs.recyclerview)
    implementation(libs.fragment.ktx)

    // Media / Playback
    implementation(libs.androidx.media)

    // Room (game library database)

    // Lifecycle
    implementation(libs.lifecycle.viewmodel.ktx)
    implementation(libs.lifecycle.livedata.ktx)
    
    // RAR extraction for RSN files (SPC archives)

    testImplementation(libs.junit)
    androidTestImplementation(libs.androidx.junit)
    androidTestImplementation(libs.androidx.espresso.core)
}
